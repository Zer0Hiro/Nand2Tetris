// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/3/b/RAM512.hdl
/**
 * Memory of 512 16-bit registers.
 * If load is asserted, the value of the register selected by
 * address is set to in; Otherwise, the value does not change.
 * The value of the selected register is emitted by out.
 */
CHIP RAM512 {
    IN in[16], load, address[9];
    OUT out[16];

    PARTS:
    DMux8Way(in=load, sel=address[6..8], a=ain, b=bin, c=cin, d=din, e=ein, f=fin, g=gin, h=hin);
    RAM64(in=in, load=ain, address=address[0..5], out=outa);
    RAM64(in=in, load=bin, address=address[0..5], out=outb);
    RAM64(in=in, load=cin, address=address[0..5], out=outc);
    RAM64(in=in, load=din, address=address[0..5], out=outd);
    RAM64(in=in, load=ein, address=address[0..5], out=oute);
    RAM64(in=in, load=fin, address=address[0..5], out=outf);
    RAM64(in=in, load=gin, address=address[0..5], out=outg);
    RAM64(in=in, load=hin, address=address[0..5], out=outh);
    Mux8Way16(a=outa, b=outb, c=outc, d=outd, e=oute, f=outf, g=outg, h=outh, sel=address[6..8], out=out);
}